
## model specification by EU

model {
  for (i in 1:N){ 
    for (j in 1:items){		
      
      # item responses
      y[i, j] ~ dbern(prob[i, j])
      logit(prob[i, j]) <- ItemPar[j,2]*PersPar[i,1] - ItemPar[j,1]
      
      # response times
      RT[i,j] ~ dlnorm(muOfLogRT[i,j] , 1/errVar[j])
      muOfLogRT[i,j] <-  ItemPar[j,3]-ItemPar[j,4]*PersPar[i,2]	
      
    }
    
    
    # prior for person parameter
    PersPar[i,1:2] ~ dmnorm(muP, invSigmaP)
  }
  
  #### hyperprior for person parameter
  # identification of model via fixed hypepriors
  muP <- c(0, 0) 
  SigmaP[1, 1] <- 1
  ## ----------------------------------
  # variance/precisions of second person parameter (speed) gets uninformative prior
  invVar ~ dgamma(0.01,0.01) 
  SigmaP[2, 2] <- inverse(invVar)
  ## ----------------------------------
  # limit covariances so can't get bigger than variances combined
  cov_bord <- sqrt(SigmaP[1, 1] * SigmaP[2, 2]) - 0.05
  rho ~ dnorm(0,0.0001) I(-cov_bord,cov_bord)
  # rho ~ dnorm(0,0.0001) I(-0.1,0.1)
  
  SigmaP[1, 2] <- rho
  SigmaP[2, 1] <- rho
  
  # invert covariance matrix (into Precision)
  invSigmaP[1:2,1:2] <- inverse(SigmaP[ , ])
  
  
  # prior for item parameter
  for (j in 1:items){
    errVar[j] ~ dlnorm(muLogErrVar, invVarErrVar)
  }
  
  for (j in 1:(items-1)){
    ItemPar[j,1:4] ~ dmnorm(muI, omegaI) 
  }
  
  ## ----------------------------------
  # last item parameters
  ItemPar[items,1:3] ~ dmnorm(muI[1:3], omegaI_2) 
  
  ItemPar[items,4] <- 1/prod(ItemPar[1:(items-1), 4])
  ## ----------------------------------
  
  ##### hyperprior for itemparameter
  muI[1] ~ dnorm(0, 0.000001)
  muI[2] ~ dnorm(1, 0.000001)
  muI[3] ~ dnorm(1, 0.000001)
  muI[4] ~ dnorm(1, 0.000001)
  
  muLogErrVar ~ dnorm(0, 0.000001)
  
  invVarErrVar ~ dgamma(0.01, 0.001)

  omegaI ~ dwish(M,5)
  
  ## ----------------------------------
  # different omega for last item parameters
  omegaI_2 ~ dwish(M[1:3, 1:3],4)
  ## ----------------------------------
  
  ### Calculate posterior distributions for desired analysis parameters
  VarErrVar <- 1/invVarErrVar
  
  SigmaI <- inverse(omegaI)
  
  for(i in 1:4){
    for(j in 1:4){
      correlI[i,j]<-SigmaI[i,j]/(sqrt(SigmaI[i,i]*SigmaI[j,j]))
    }
  }
  
}
